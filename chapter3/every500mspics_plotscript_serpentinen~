reset
set key below
set terminal epslatex color size 6,5
set output "chapter3/every500mspics_serpentinen.tex"
set xrange [-10:10]
set yrange [-10:10]
set size ratio 1.0
set zeroaxis
set border 0
set grid 
set xtics -6,6,6
set ytics -6,6,6
set tics scale 0.5
set xlabel "X-Achse in Metern"
set ylabel "Y-Achse in Metern"
set object 1 rect from -6,-6 to 6,6 fs empty border rgb "grey" back
set object 2 rect from -6,6 to 2,6.2 fc rgb "black"
set object 3 rect from 6,-2 to 6.2,6 fc rgb "black"
set object 4 rect from -2,-6 to 6,-6.2 fc rgb "black"
set style line 11 lc rgb 'red' lt 1
set style line 12 lc rgb 'blue' lt 2 
plot \
'chapter3/every500mspics_serpentinen' every 50::1 using 3:4:5:6:7 t "$3\\sigma$" with ellipses lc rgb "blue",\
'chapter3/every500mspics_serpentinen' every 50::1 using 3:4 t "" with points lc rgb "blue",\
'chapter3/every500mspics_serpentinen' every ::1 using 1:2 t "Trajektorie des Roboters" with lines ls 11,\
'chapter3/every500mspics_serpentinen' every ::1 using 3:4 t "Filter Sch\344tzung" with lines ls 12,\
'chapter3/every500mspics_serpentinen' every ::1 using 8:2 t "Update" ls 11

